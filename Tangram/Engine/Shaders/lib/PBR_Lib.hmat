#include "Light_Def.hmat"
#include "Basic_FS_Lib.hmat"

float D_GGX_TR(vec3 N, vec3 H, float a)
{
    float a2 = a*a;
    a2 *= a2;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float k)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, k);
    float ggx2 = GeometrySchlickGGX(NdotL, k);

    return ggx1 * ggx2;
}

vec3 PBR_Lo(vec3 N, vec3 V, vec3 L, vec3 radiance, vec3 albedo, float metallic, float roughness)
{
    vec3 H = normalize(V + L);
    
    // cook-torrance brdf
    float NDF = D_GGX_TR(N, H, roughness);
    float G = GeometrySmith(N, V, L, roughness);
    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);
    vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);

    vec3 kS = F;
    vec3 kD = vec3(1.0) - kS;
    kD *= 1.0 - metallic;

    vec3 nominator = NDF * G * F;
    float denominator = 4 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001; 
    vec3 specular = nominator / denominator;

    // add to outgoing radiance Lo
    float NdotL = max(dot(N, L), 0.0);
    return (kD * albedo / PI + specular) * radiance * NdotL;
}

vec3 PBR_Lo_DirectLight(DirectLight directLight, vec3 N, vec3 V, vec3 albedo, float metallic, float roughness)
{
    vec3 L = normalize(directLight.direction);
    vec3 radiance = directLight.color * directLight.intensity;
    return PBR_Lo(N, V, L, radiance, albedo, metallic, roughness);
}

vec3 PBR_Lo_PointLight(PointLight pointLight, vec3 worldPos, vec3 N, vec3 V, vec3 albedo, float metallic, float roughness)
{
    vec3 L = normalize(pointLight.pos - worldPos);
    float distance = length(pointLight.pos - worldPos);
    float attenuation = 1.0 / (distance * distance);
    vec3 radiance = pointLight.color * pointLight.intensity * attenuation;
    return PBR_Lo(N, V, L, radiance, albedo, metallic, roughness);
}

vec3 PBR_Ambient(vec3 albedo, float ao)
{
    return vec3(0.03) * albedo * ao;
}